# Copyright (c) 2017-2018, NVIDIA CORPORATION. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

project(dali_core CUDA CXX C)

add_subdirectory(mm)
add_subdirectory(os)

# Get all the source files
collect_headers(DALI_INST_HDRS PARENT_SCOPE)
collect_sources(DALI_CORE_SRCS)
collect_test_sources(DALI_CORE_TEST_SRCS)

# transform a list of paths into a list of include directives
DETERMINE_GCC_SYSTEM_INCLUDE_DIRS("c++" "${CMAKE_CXX_COMPILER}" "${CMAKE_CXX_FLAGS}" INFERED_COMPILER_INCLUDE)
set(DEFAULT_COMPILER_INCLUDE)
foreach(incl_dir ${INFERED_COMPILER_INCLUDE})
  set(DEFAULT_COMPILER_INCLUDE "${DEFAULT_COMPILER_INCLUDE} -I${incl_dir}")
endforeach(incl_dir)
separate_arguments(DEFAULT_COMPILER_INCLUDE UNIX_COMMAND  "${DEFAULT_COMPILER_INCLUDE}")

if (NOT LINK_DRIVER)
  set(CUDA_GENERATED_STUB "${CMAKE_CURRENT_BINARY_DIR}/dynlink_cuda_gen.cc")
  add_custom_command(
      OUTPUT ${CUDA_GENERATED_STUB}
      COMMAND python ${CMAKE_CURRENT_SOURCE_DIR}/../../tools/stub_generator/stub_codegen.py --unique_prefix=Cuda --
                  "${CMAKE_CURRENT_SOURCE_DIR}/../../tools/stub_generator/cuda.json" ${CUDA_GENERATED_STUB}
                  "${CMAKE_CUDA_TOOLKIT_INCLUDE_DIRECTORIES}/cuda.h" "-I${CMAKE_CUDA_TOOLKIT_INCLUDE_DIRECTORIES}"
                  # for some reason QNX fails with 'too many errors emitted' is this is not set
                  "-ferror-limit=0"
                  ${DEFAULT_COMPILER_INCLUDE}
      DEPENDS ${CMAKE_CURRENT_SOURCE_DIR}/../../tools/stub_generator/stub_codegen.py
              "${CMAKE_CUDA_TOOLKIT_INCLUDE_DIRECTORIES}/cuda.h"
              "${CMAKE_CURRENT_SOURCE_DIR}/../../tools/stub_generator/cuda.json"
      COMMENT "Running cuda.h stub generator"
      VERBATIM)

  set_source_files_properties(${CUDA_GENERATED_STUB} PROPERTIES GENERATED TRUE)
  add_library(dynlink_cuda STATIC dynlink_cuda.cc ${CUDA_GENERATED_STUB})
else()
  add_library(dynlink_cuda STATIC dynlink_cuda.cc)
endif()

list(FILTER DALI_CORE_SRCS EXCLUDE REGEX ".*dynlink_cuda.cc")

if (BUILD_CUFILE)
  set(CUFILE_GENERATED_STUB "${CMAKE_CURRENT_BINARY_DIR}/dynlink_cufile_gen.cc")
  add_custom_command(
      OUTPUT ${CUFILE_GENERATED_STUB}
      COMMAND python ${CMAKE_CURRENT_SOURCE_DIR}/../../tools/stub_generator/stub_codegen.py --unique_prefix=Cufile --
                  "${CMAKE_CURRENT_SOURCE_DIR}/../../tools/stub_generator/cufile.json" ${CUFILE_GENERATED_STUB}
                  "${CMAKE_CUDA_TOOLKIT_INCLUDE_DIRECTORIES}/cufile.h" "-I${CMAKE_CUDA_TOOLKIT_INCLUDE_DIRECTORIES}"
                  # for some reason QNX fails with 'too many errors emitted' is this is not set
                  "-ferror-limit=0"
                  ${DEFAULT_COMPILER_INCLUDE}
      DEPENDS ${CMAKE_CURRENT_SOURCE_DIR}/../../tools/stub_generator/stub_codegen.py
              "${CMAKE_CUDA_TOOLKIT_INCLUDE_DIRECTORIES}/cufile.h"
              "${CMAKE_CURRENT_SOURCE_DIR}/../../tools/stub_generator/cufile.json"
      COMMENT "Running cufile.h stub generator"
      VERBATIM)

  set_source_files_properties(${CUFILE_GENERATED_STUB} PROPERTIES GENERATED TRUE)
  add_library(dynlink_cufile STATIC dynlink_cufile.cc ${CUFILE_GENERATED_STUB})
endif()

list(FILTER DALI_CORE_SRCS EXCLUDE REGEX ".*dynlink_cufile.cc")

adjust_source_file_language_property("${DALI_CORE_SRCS}")
add_library(dali_core ${LIBTYPE} ${DALI_CORE_SRCS})
target_include_directories(dali_core SYSTEM PUBLIC ${CMAKE_CUDA_TOOLKIT_INCLUDE_DIRECTORIES})
target_link_libraries(dali_core PRIVATE dynlink_cuda ${CUDART_LIB})
if (BUILD_CUFILE)
    target_link_libraries(dali_core PRIVATE dynlink_cufile)
  endif()
target_link_libraries(dali_core PUBLIC ${DALI_SYSTEM_LIBS})
target_link_libraries(dali_core PRIVATE "-Wl,--exclude-libs,${exclude_libs}")
set_target_properties(dali_core PROPERTIES
    LIBRARY_OUTPUT_DIRECTORY "${DALI_LIBRARY_OUTPUT_DIR}")

set(lib_exports "libdali_core.map")
configure_file("${DALI_ROOT}/cmake/${lib_exports}.in" "${CMAKE_BINARY_DIR}/${lib_exports}")
target_link_libraries(dali_core PRIVATE -Wl,--version-script=${CMAKE_BINARY_DIR}/${lib_exports})

if (BUILD_TEST)
  adjust_source_file_language_property("${DALI_CORE_TEST_SRCS}")
  add_executable(dali_core_test "${DALI_CORE_TEST_SRCS}")
  target_link_libraries(dali_core_test PUBLIC dali_core)
  target_link_libraries(dali_core_test PRIVATE gtest dynlink_cuda ${DALI_LIBS})
  if (BUILD_CUFILE)
    target_link_libraries(dali_core_test PRIVATE dynlink_cufile)
  endif()
  target_link_libraries(dali_core_test PRIVATE "-Wl,--exclude-libs,${exclude_libs}")
  target_link_libraries(dali_core_test PRIVATE "-pie")
  set_target_properties(dali_core_test PROPERTIES POSITION_INDEPENDENT_CODE ON)
  set_target_properties(dali_core_test PROPERTIES OUTPUT_NAME "dali_core_test.bin")

  set_target_properties(dali_core_test PROPERTIES
    RUNTIME_OUTPUT_DIRECTORY ${TEST_BINARY_DIR})

  add_check_gtest_target("check-core-gtest" dali_core_test ${TEST_BINARY_DIR})
endif()
